buildscript {
    ext {
        springBootVersion = '1.3.0.M3'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'application'

jar {
    baseName = 'jvm-bloggers'
    version = '0.1.0'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}


dependencies {
    // Spring Boot stuff
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-devtools"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile 'com.github.ulisesbocchio:jasypt-spring-boot:1.2'


    // Logic
    compile 'com.typesafe.akka:akka-actor_2.10:2.3.12'
    compile 'com.typesafe.akka:akka-slf4j_2.10:2.3.12'
    compile 'rome:rome:1.0'


    // Utils and helpers
    compile "org.projectlombok:lombok:1.16.0"
    compile 'com.google.guava:guava:18.0'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.antlr:ST4:4.0.7'
    compile 'com.sendgrid:sendgrid-java:2.2.2'

    // Database related
    compile "org.postgresql:postgresql:9.4-1201-jdbc41"
    compile 'org.liquibase:liquibase-core:3.3.1'

    // Test dependencies
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'org.spockframework:spock-spring:0.7-groovy-2.0'
    testCompile 'cglib:cglib-nodep:2.2.2'
    testCompile 'org.codehaus.groovy:groovy:2.3.8'
    runtime 'com.h2database:h2'

}

checkstyle {
    toolVersion = "6.5"
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.coveralls {
    dependsOn 'check'
}

task stage(dependsOn: ['clean', 'build'])

wrapper {
    gradleVersion "2.7"
}

